# Copyright 2020, 2021, 2022, 2023, 2024 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@bazel_skylib//lib:selects.bzl", "selects")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@bazel_skylib//rules:expand_template.bzl", "expand_template")

package(
    default_applicable_licenses = ["//:license"],
    default_visibility = ["//visibility:private"],
)

licenses(["notice"])

bzl_library(
    name = "defs_bzl",
    srcs = ["defs.bzl"],
    visibility = [
        "//docs:__pkg__",
        "//elisp:__pkg__",
        "//emacs:__pkg__",
    ],
    deps = [
        ":generated_bzl",
        "//elisp:builtin_bzl",
        "@bazel_skylib//lib:paths",
        "@bazel_skylib//lib:versions",
        "@phst_rules_elisp_deps//:defs_bzl",
    ],
)

bzl_library(
    name = "repositories_bzl",
    srcs = ["repositories.bzl"],
    visibility = [
        "//docs:__pkg__",
        "//elisp:__pkg__",
    ],
)

bzl_library(
    name = "extensions_bzl",
    srcs = ["extensions.bzl"],
    deps = [":repositories_bzl"],
)

bzl_library(
    name = "generated_bzl",
    srcs = ["generated.bzl"],
)

expand_template(
    name = "expand",
    out = "generated.expanded.bzl",
    substitutions = {
        "[[chr]]": ", ".join(['"\\%o"' % i for i in range(0x100)]),
        "[[ord]]": ", ".join(['"\\%o": %d' % (i, i) for i in range(0x100)]),
    },
    template = "generated.template.bzl",
)

diff_test(
    name = "generated_test",
    failure_message = """Please run

    bazel build //private:generated.expanded.bzl

and copy the generated file to //private:generated.bzl.""",
    file1 = "generated.bzl",
    file2 = "generated.expanded.bzl",
)

selects.config_setting_group(
    name = "gcc_or_clang",
    match_any = [
        ":gcc",
        ":clang",
    ],
    visibility = ["//visibility:private"],
)

selects.config_setting_group(
    name = "gcc",
    match_any = [
        ":gcc_new",
        ":gcc_old",
    ],
    visibility = ["//visibility:private"],
)

config_setting(
    name = "gcc_new",
    flag_values = {"@bazel_tools//tools/cpp:compiler": "gcc"},
    visibility = [
        "//elisp:__pkg__",
        "//elisp/proto:__pkg__",
        "//emacs:__pkg__",
    ],
)

# Obsolete value for the C++ compiler flag, to be removed once
# https://github.com/bazelbuild/bazel/pull/16297 and
# https://github.com/bazelbuild/bazel/pull/20350 are in all supported Bazel
# versions.
config_setting(
    name = "gcc_old",
    flag_values = {"@bazel_tools//tools/cpp:compiler": "compiler"},
    visibility = [
        "//elisp:__pkg__",
        "//elisp/proto:__pkg__",
        "//emacs:__pkg__",
    ],
)

config_setting(
    name = "clang",
    flag_values = {"@bazel_tools//tools/cpp:compiler": "clang"},
    visibility = [
        "//elisp:__pkg__",
        "//elisp/proto:__pkg__",
        "//emacs:__pkg__",
    ],
)

config_setting(
    name = "msvc-cl",
    flag_values = {"@bazel_tools//tools/cpp:compiler": "msvc-cl"},
    visibility = [
        "//elisp:__pkg__",
        "//elisp/proto:__pkg__",
        "//emacs:__pkg__",
    ],
)
