# Copyright 2023, 2024, 2025 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

"""Bzlmod module definition for rules_elisp."""

module(
    name = "phst_rules_elisp",
    bazel_compatibility = [">=7.2.1"],
)

# Non-development module dependencies
bazel_dep(name = "platforms", version = "0.0.10")
bazel_dep(name = "bazel_skylib", version = "1.7.1")
bazel_dep(name = "rules_license", version = "1.0.0")
bazel_dep(name = "rules_cc", version = "0.1.0")
bazel_dep(name = "rules_python", version = "1.0.0")
bazel_dep(name = "abseil-cpp", version = "20240722.0.bcr.2")
bazel_dep(name = "protobuf", version = "29.2")

# Work around https://github.com/bazelbuild/bazel/issues/24426.  See
# https://github.com/bazelbuild/bazel-central-registry/pull/3320#issuecomment-2546030208.
# FIXME: Remove once fixed upstream in googletest and abseil-cpp.
bazel_dep(name = "re2", version = "2024-07-02.bcr.1")

# Development-only module dependencies
bazel_dep(name = "stardoc", version = "0.7.2", dev_dependency = True)
bazel_dep(name = "rules_go", version = "0.51.0", dev_dependency = True)
bazel_dep(name = "abseil-py", version = "2.1.0", dev_dependency = True)
bazel_dep(name = "gazelle", version = "0.40.0", dev_dependency = True)

# Bring in the local_config_cc repository.  Itâ€™s needed for
# //emacs:windows_cc_toolchain.
cc_configure = use_extension("@rules_cc//cc:extensions.bzl", "cc_configure_extension")
use_repo(cc_configure, "local_config_cc", "local_config_cc_toolchains")

# Non-module dependencies
deps = use_extension("//private:extensions.bzl", "deps")
use_repo(deps, "local_emacs")
use_repo(deps, "gnu_emacs_28.2")
use_repo(deps, "gnu_emacs_29.4")
use_repo(deps, "gnu_emacs_windows_28.2")
use_repo(deps, "gnu_emacs_windows_29.4")

dev_deps = use_extension("//private:extensions.bzl", "dev_deps", dev_dependency = True)
use_repo(dev_deps, "phst_rules_elisp_dev_deps")

# Local toolchains
register_toolchains("@phst_rules_elisp//elisp:hermetic_toolchain")

# C++-specific dependencies
register_toolchains("@local_config_cc_toolchains//:all")

# Python-specific dependencies
python = use_extension("@rules_python//python/extensions:python.bzl", "python", dev_dependency = True)
python.toolchain(
    configure_coverage_tool = True,
    # Attempt to work around nondeterminism on Windows.  See
    # https://github.com/bazelbuild/rules_python/pull/713 and
    # https://github.com/bazelbuild/rules_python/pull/907.
    ignore_root_user_error = True,
    python_version = "3.10",
)
use_repo(python, hermetic_python = "python_3_10")

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip", dev_dependency = True)
pip.parse(
    hub_name = "pip",
    python_version = "3.10",
    requirements_darwin = "//dev:requirements-macos.lock",
    requirements_linux = "//dev:requirements-linux.lock",
    requirements_windows = "//dev:requirements-windows.lock",
)
use_repo(pip, pip_deps = "pip")

# Go-specific dependencies
go_sdk = use_extension("@rules_go//go:extensions.bzl", "go_sdk", dev_dependency = True)
go_sdk.nogo(nogo = "//dev:nogo")

go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps", dev_dependency = True)
go_deps.module(
    path = "github.com/bazelbuild/buildtools",
    sum = "h1:FGzENZi+SX9I7h9xvMtRA3rel8hCEfyzSixteBgn7MU=",
    version = "v0.0.0-20240918101019-be1c24cc9a44",
)
go_deps.module(
    path = "github.com/google/addlicense",
    sum = "h1:jpVf9qPbU8rz5MxKo7d+RMcNHkqxi4YJi/laauX4aAE=",
    version = "v1.1.1",
)
go_deps.module(
    path = "github.com/google/go-cmp",
    sum = "h1:ofyhxvXcZhMsU5ulbFiLKl/XBFqE1GSq7atu8tAmTRI=",
    version = "v0.6.0",
)
use_repo(
    go_deps,
    "com_github_bazelbuild_buildtools",
    "com_github_google_addlicense",
    "com_github_google_go_cmp",
)

# Tools for development
bazel_dep(name = "buildifier_prebuilt", version = "7.3.1", dev_dependency = True)
bazel_dep(name = "hedron_compile_commands", dev_dependency = True)
archive_override(
    module_name = "hedron_compile_commands",
    integrity = "sha384-esPTYvOAWfvydCLlfTFTItuRKaDDLV/qJ+3BRqlh6pGhvhOmJtqKZ8OeyJ7iuSUy",
    strip_prefix = "bazel-compile-commands-extractor-eca42c63700fccdc49cf58177e0a96f0f6075a68",
    urls = ["https://github.com/hedronvision/bazel-compile-commands-extractor/archive/eca42c63700fccdc49cf58177e0a96f0f6075a68.zip"],
)

bazel_dep(name = "phst_update_workspace_snippets", dev_dependency = True)
archive_override(
    module_name = "phst_update_workspace_snippets",
    integrity = "sha384-hARfrBeNuJMLiv/SMWGIZci4JM+9wZu/h1rCYmVqfGVeWh65vvLqdyvCIDvz82kB",
    strip_prefix = "update-workspace-snippets-fba2b14f5a8280f65a313b9037ccecb60e33f238",
    urls = ["https://github.com/phst/update-workspace-snippets/archive/fba2b14f5a8280f65a313b9037ccecb60e33f238.zip"],
)

bazel_dep(name = "phst_bazelcov", dev_dependency = True)
archive_override(
    module_name = "phst_bazelcov",
    integrity = "sha384-9W8IIOl1LVX9JbpK6MY4Uu2mf62i+hrmghW4hmCtLO8ABYqPTcQf8Oo0t4VUnznK",
    strip_prefix = "bazelcov-012a9b2b7211b37e886403ba284b1b09e6665e89",
    urls = ["https://github.com/phst/bazelcov/archive/012a9b2b7211b37e886403ba284b1b09e6665e89.zip"],
)

bazel_dep(name = "phst_merge_bazel_lockfiles", dev_dependency = True)
archive_override(
    module_name = "phst_merge_bazel_lockfiles",
    integrity = "sha384-gFuWEcT8E1bI6t3DVCcdr3wPUQ2IZOY9ND472JvDb55pZhLKJd9j6bvjDubP7svW",
    strip_prefix = "merge-bazel-lockfiles-95005999479dcac5d6a3399a9f58625e078db448",
    urls = ["https://github.com/phst/merge-bazel-lockfiles/archive/95005999479dcac5d6a3399a9f58625e078db448.zip"],
)

# We want to test the external example without adding its repository to the
# registry.  So we override the dependency immediately locally.
bazel_dep(name = "phst_rules_elisp_example", dev_dependency = True)
local_path_override(
    module_name = "phst_rules_elisp_example",
    path = "examples/ext",
)
