# Copyright 2023, 2024, 2025 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

"""Bzlmod module definition for rules_elisp."""

module(
    name = "phst_rules_elisp",
    bazel_compatibility = [">=7.2.1"],
)

# Non-development module dependencies
bazel_dep(name = "platforms", version = "0.0.11")
bazel_dep(name = "bazel_skylib", version = "1.7.1")
bazel_dep(name = "rules_license", version = "1.0.0")
bazel_dep(name = "rules_cc", version = "0.1.1")
bazel_dep(name = "rules_python", version = "1.3.0")
bazel_dep(name = "rules_shell", version = "0.4.1")
bazel_dep(name = "abseil-cpp", version = "20250512.0")
bazel_dep(name = "protobuf", version = "31.0")

# Work around https://github.com/bazelbuild/bazel/issues/24426.  See
# https://github.com/bazelbuild/bazel-central-registry/pull/3320#issuecomment-2546030208.
# FIXME: Remove once fixed upstream in googletest and abseil-cpp.
bazel_dep(name = "re2", version = "2024-07-02.bcr.1")

# Work around https://github.com/bazelbuild/rules_apple/issues/2660.
# FIXME: Remove once fixed upstream in the protobuf module.
bazel_dep(name = "rules_apple", version = "3.22.0")

# Work around missing swiftc error on Windows.  See
# https://github.com/bazelbuild/rules_swift/pull/1483.
# FIXME: Remove once fixed upstream in the rules_apple module.
bazel_dep(name = "rules_swift", version = "2.8.2")

# Development-only module dependencies
bazel_dep(name = "stardoc", version = "0.8.0", dev_dependency = True)
bazel_dep(name = "rules_go", version = "0.54.1", dev_dependency = True)
bazel_dep(name = "abseil-py", version = "2.1.0", dev_dependency = True)
bazel_dep(name = "gazelle", version = "0.43.0", dev_dependency = True)
bazel_dep(name = "bazel_skylib_gazelle_plugin", version = "1.7.1", dev_dependency = True)
bazel_dep(name = "bazel_features", version = "1.30.0", dev_dependency = True)

# Bring in the local_config_cc repository.  Itâ€™s needed for
# //emacs:windows_cc_toolchain.
cc_configure = use_extension("@rules_cc//cc:extensions.bzl", "cc_configure_extension")
use_repo(cc_configure, "local_config_cc", "local_config_cc_toolchains")

# Non-module dependencies
emacs_repository = use_repo_rule("//elisp/private:emacs_repository.bzl", "emacs_repository")

emacs_repository(
    name = "gnu_emacs_29.4",
    integrity = "sha384-1LIwxBtAr9RzK3VJ359OfOh/PN83fyfl7uckmMw+Z0mKabbOOsvy00PhXvm5wJtf",
    mode = "source",
    output = "emacs.tar.xz",
    strip_prefix = "emacs-29.4",
    urls = [
        "https://ftpmirror.gnu.org/emacs/emacs-29.4.tar.xz",
        "https://ftp.gnu.org/gnu/emacs/emacs-29.4.tar.xz",
    ],
)

emacs_repository(
    name = "gnu_emacs_windows_29.4",
    integrity = "sha384-wu5kKCCMX6BLgSoUfMEUf1gLk4Ua+rWa8mldAeW+Y6q+RXyCmdPZP/XuJPO9uWrt",
    mode = "release",
    output = "emacs.zip",
    strip_prefix = "",
    target_compatible_with = [
        "@platforms//os:windows",
        "@platforms//cpu:x86_64",
    ],
    urls = [
        "https://ftpmirror.gnu.org/emacs/windows/emacs-29/emacs-29.4.zip",
        "https://ftp.gnu.org/gnu/emacs/windows/emacs-29/emacs-29.4.zip",
    ],
)

emacs_repository(
    name = "gnu_emacs_30.1",
    integrity = "sha384-2K37C4VadoRIlCTmMWJA75AtWC05CrVQ4D80EpbAu1Fyk9MXLgbIbxf0CFjcZ0jC",
    mode = "source",
    output = "emacs.tar.xz",
    strip_prefix = "emacs-30.1",
    urls = [
        "https://ftpmirror.gnu.org/emacs/emacs-30.1.tar.xz",
        "https://ftp.gnu.org/gnu/emacs/emacs-30.1.tar.xz",
    ],
)

emacs_repository(
    name = "gnu_emacs_windows_30.1",
    integrity = "sha384-eRRAnADE9t62y4bk8HyAi4SM1WoygTtysP+PjN8bp18LsB9ZBIsQe16w5+8XTkpc",
    mode = "release",
    output = "emacs.zip",
    strip_prefix = "",
    target_compatible_with = [
        "@platforms//os:windows",
        "@platforms//cpu:x86_64",
    ],
    urls = [
        "https://ftpmirror.gnu.org/emacs/windows/emacs-30/emacs-30.1.zip",
        "https://ftp.gnu.org/gnu/emacs/windows/emacs-30/emacs-30.1.zip",
    ],
)

local_emacs_repository = use_repo_rule("//elisp/private:local_emacs_repository.bzl", "local_emacs_repository")

local_emacs_repository(name = "local_emacs")

junit_xsd = use_repo_rule("//private:junit_xsd.bzl", "junit_xsd")

junit_xsd(
    name = "junit_xsd",
    dev_dependency = True,
)

# Local toolchains
register_toolchains("//elisp:hermetic_toolchain")

# C++-specific dependencies
register_toolchains("@local_config_cc_toolchains//:all")

# Python-specific dependencies
python = use_extension("@rules_python//python/extensions:python.bzl", "python", dev_dependency = True)
python.toolchain(
    configure_coverage_tool = True,
    # Attempt to work around nondeterminism on Windows.  See
    # https://github.com/bazelbuild/rules_python/pull/713 and
    # https://github.com/bazelbuild/rules_python/pull/907.
    ignore_root_user_error = True,
    python_version = "3.12",
)

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip", dev_dependency = True)
pip.parse(
    hub_name = "pip",
    python_version = "3.12",
    requirements_darwin = "//dev:requirements-macos.lock",
    requirements_linux = "//dev:requirements-linux.lock",
    requirements_windows = "//dev:requirements-windows.lock",
)
use_repo(pip, "pip")

# Work around https://github.com/bazel-contrib/rules_python/issues/2762.
# FIXME: Remove once https://github.com/bazel-contrib/rules_python/issues/2762
# is fixed upstream.
single_version_override(
    module_name = "rules_python",
    version = "1.2.0",
)

# Go-specific dependencies
go_sdk = use_extension("@rules_go//go:extensions.bzl", "go_sdk", dev_dependency = True)
go_sdk.nogo(nogo = "//dev:nogo")

go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps", dev_dependency = True)
go_deps.from_file(go_mod = "//:go.mod")
use_repo(
    go_deps,
    "com_github_bazelbuild_buildtools",
    "com_github_google_go_cmp",
)

# Tools for development
bazel_dep(name = "buildifier_prebuilt", version = "8.0.3", dev_dependency = True, repo_name = "buildifier")
bazel_dep(name = "phst_license_test", version = "0", dev_dependency = True)
git_override(
    module_name = "phst_license_test",
    commit = "f135357cffb46e2b1120623e15eec7c88e7f34d4",
    remote = "https://github.com/phst/license_test.git",
)

# Bogus versions added because of
# https://github.com/renovatebot/renovate/issues/33476.
# FIXME: Remove them once Renovate issue is fixed.
bazel_dep(name = "hedron_compile_commands", version = "0", dev_dependency = True)
git_override(
    module_name = "hedron_compile_commands",
    commit = "4f28899228fb3ad0126897876f147ca15026151e",
    remote = "https://github.com/hedronvision/bazel-compile-commands-extractor.git",
)

bazel_dep(name = "phst_update_workspace_snippets", version = "0", dev_dependency = True, repo_name = "update-workspace-snippets")
git_override(
    module_name = "phst_update_workspace_snippets",
    commit = "35571250b498029c928fddc48186ce10df0729ed",
    remote = "https://github.com/phst/update-workspace-snippets.git",
)

bazel_dep(name = "phst_bazelcov", version = "0", dev_dependency = True, repo_name = "bazelcov")
git_override(
    module_name = "phst_bazelcov",
    commit = "427cc675e1dff2b4d565321c04463a63686420e0",
    remote = "https://github.com/phst/bazelcov.git",
)

bazel_dep(name = "phst_merge_bazel_lockfiles", version = "0", dev_dependency = True)
git_override(
    module_name = "phst_merge_bazel_lockfiles",
    commit = "824e2ea2457c586a6b8b71676b4d806dae629ed7",
    remote = "https://github.com/phst/merge-bazel-lockfiles.git",
)

# We want to test the external example without adding its repository to the
# registry.  So we override the dependency immediately locally.
bazel_dep(name = "phst_rules_elisp_example", version = "0", dev_dependency = True)
local_path_override(
    module_name = "phst_rules_elisp_example",
    path = "examples/ext",
)
